\input texinfo    @c -*- texinfo -*-
@c %**start of header
@setfilename arbel.info
@settitle ARBEL
@documentencoding UTF-8
@documentlanguage en
@c %**end of header

@finalout
@titlepage
@title ARBEL
@author Zach Flynn
@end titlepage

@contents

@ifnottex
@node Top
@top ARBEL
@end ifnottex

@menu
* Introduction::
* The Basics::
* Examples::
* Reference::

@detailmenu
--- The Detailed Node Listing ---

Introduction

* ARBEL is a Registry-Based Environment and Language::
* Distinguishing Features::

The Basics

* Statements and syntax::
* Registries::
* Registers::
* Environment::

Registries

* Additional details about colon notation::

Examples

* A Line-Oriented Text Editor::
* Numerical Differentiation::
* Dispatch::
* Compute Factorial::
* Numerical Integration::
* Basic Linear Algebra::
* Split a String::

Reference

* Registry operations::
* Real and Integer operations::
* Control flow operations::
* Environment operations::
* String operations::
* Register operations::
* Instruction operations::
* File operations::
* Boolean operations::
* Column operations::

Registry operations

* Creation::
* Insert, move, and remove data to and from registers: Insert move and remove data to and from registers. 
* Access data in Registry::
* Apply Instructions to elements of a Registry::
* Execute code in a registry::
* Test if a registry::
* Classify a registry::

Real and Integer operations

* Arithmetic operations::
* Comparison operations::
* Conversion operations::
* Test if type operations::
* Common mathematical operations::

Environment operations

* Modify the /ans register::
* Exit ARBEL::
* Print output::
* Input and output files and state::
* Interacting with the shell::
* Error handling::

String operations

* Access, search, and modify string elements: Access search and modify string elements. 
* String properties::
* String comparison::
* Combine strings::
* Convert to string::
* Test if String::
* Strings from User Input::

Register operations

* Operations on "list" registers::
* Convert to register::
* Register comparison::
* Test if a Register::

@end detailmenu
@end menu

@node Introduction
@chapter Introduction

@menu
* ARBEL is a Registry-Based Environment and Language::
* Distinguishing Features::
@end menu

@node ARBEL is a Registry-Based Environment and Language
@section ARBEL is a Registry-Based Environment and Language

ARBEL is a Registry-Based Environment and Language. A @emph{registry} is a data structure where data is located at "registers".  The data in the registers can be other registries or numbers or strings or code.  It is what other languages might call a @emph{map} or a (hash) @emph{table}.  Registries are the fundamental data type of ARBEL and the language provides many operations to manipulate and recover values from the registries.  ARBEL instructions operate @emph{within} a certain registry.  The @emph{environment} (which registers you can access, and so, which objects and instructions are available) is determined by which registry ARBEL is currently operating within.  Instructions themselves are nothing more than a way to create a new (temporary) registry, execute some code inside of it, and write results to the current registry.  

From this basic, but flexible, data type ARBEL builds up a useful system for interactively constructing programs.  It's goal is to be easy to use interactively for building @emph{personal} programs (programs primarily for the user's own use) which is why the syntax is very similar to entering commands on the command line.  There are commands to save the code you have already entered so that you can interactively build scripts.  It also has commands that make it easy to save state and the registries that you have created to disk so that you can pick up where you left off.

@node Distinguishing Features
@section Distinguishing Features

ARBEL's first distinguishing feature is that it is @emph{naturally generic}.  What this means is that any non-literal in an instruction can be replaced by the user without the author of the instruction anticipating that it would be useful for the user to replace that part of the instruction.  This allows the user a large degree of control over code that other's have written.  This works because instructions in ARBEL do not really take "arguments" like in other languages, they take "environments" (registries) that put different values at different registers.  Because every aspect of the environment can be replaced, instructions are naturally generic.

ARBEL's second main distinguishing feature is that it uses different parenthesis types (@samp{()}, @samp{@{@}}, and @samp{[]}) to control when substatements are evaluated.  Open parenthesis (@samp{()}) quote a set of instructions so that they can be executed in another registry besides the one that is currently being used.  Closed parenthesis (@samp{[]}) execute the substatement immediately, as if it were entered as a previous statement.  Curly parenthesis (@samp{@{@}}) execute the substatement only if the instructruction containing it, tries to access its value.

The other feature of Arbel is that it is not only a @emph{language}, it is also an @emph{environment}. It can be used interactively and its state can be written to disk so you can pick up where your left off or store commonly used instructions for later use.  This was one of my main motivations for writing the language: to create a general purpose language that is easy to use interactively and where it is easy to save state so that it can be used to do personal programming: interact with the computer with the power of a full programming language.  

The language is dynamically-typed (registers do not have types, only the values entered in the registries do), and it is strongly typed (values have a certain type and cannot be coerced to other types except explicitly).

The best way to get a feel for the language and environment is by example so@dots{}

@node The Basics
@chapter The Basics

@menu
* Statements and syntax::
* Registries::
* Registers::
* Environment::
@end menu

@node Statements and syntax
@section Statements and syntax

A single line of ARBEL code is a statement.  The first element of the statement is one of ARBEL's operators and the other elements are arguments to the operator.  Statements, like sentences, end with periods. For example,

@example
... set /hello "Hello, world!" .
@end example


@samp{set} is the ARBEL operation to set a register to a certain value.  Names that have a forward slash (â€‹/) preceeding them are @emph{registers}, like @samp{/hello} above. Strings are words enclosed in double quotation marks.  The following demonstrates the other data types in ARBEL:

@example
... set /days-in-week 7 .
... set /penny 0.01 .
... set /facts True .
... set /lies False .
... set /add-one ( add t 1 . ) .
@end example

An Integer is a number without a decimal part, a Real is a number with a decimal part, a Boolean value is either True or False, an Instruction is a collection of one or more statements quoted as code (usually created by enclosing code with open parenthesis, "(" and ")").  Instructions can be executed later inside registries perhaps different than the registries in which they were initially defined.

To use the value located in a registry, simply type the name of the register without the preceeding forward slash.  For example,
@example
... print days-in-week .
7
@end example

All code elements must be separated by whitespace in ARBEL including the period that ends the statement and parenthesis.  So it would @emph{not} be valid to write,

@example
' All are WRONG:
print days-in-week.
set /add-one (add t 1 .) .
@end example

There must be whitespace before the period.  There also must be whitespace after and before parenthesis. 

There are three parenthesis types in ARBEL: open parentheis ("()"), closed parenthesis ("[]"), and curly parenthesis ("@{@}").  All contain statements, but they differ in @emph{when} the statements they contain are executed.

@itemize
@item
Open parenthesis store the code itself as an Instruction object to be executed later using the operation @samp{call}.  For example,
@example
... set /add-one ( add t 1 . ) .
... call add-one /t 10 .
ans = 11
@end example

@samp{call} takes an instruction and then assigns each of the registers that you specify to different values. You do not need to declare what arguments a function takes and you can specify any number of registers to set for any Instruction.  For example, 
@example
... set /add-one ( add t 1 . ) .
... call add-one /t 10 .
ans = 11
... call add-one /t 10 /add sub .
ans = 9
@end example

This is a defining feature of ARBEL. Code is naturally generic. Any object in an instruction can be replaced by any other (except for a literal value like @samp{11} or @samp{"hello"} ) when the Instruction is called.  The person who wrote the code does not need to do anything to allow this.  You can redefine any non-literal in the code by adding @samp{/name value} in the call to the Instruction.

Because there is no ambiguity, you do not actually need to use the @samp{call} operation directly.  ARBEL will insert it for you if the first argument is an instruction.  So the following would work as well,
@example
... add-one /t 10 .
ans = 11
@end example

Instruction objects can also be used in the @samp{while} looping operation and in several other operations.  Their main purpose is to be evaluated later in a potentially different and separate registry.

@item
Closed parenthesis ("[]") execute the statements inside of them immediately and evaluate to whatever the code sets the @samp{/ans} register to.  The purpose of these statements is to avoid needing to define temporary registers for intermediate calculations.  For example,
@example
... add [ sub 5 3 . ] [ mul 2 3 . ] .
ans = 8
@end example

You may have noticed the @samp{ans = ...} after entering a lot of these commands.  This tells you the current value of the @samp{/ans} register which is special in ARBEL.  Operations often write to the @samp{/ans} register in the registry from which they are called.  It is the way to "return" values after calling an operation or instruction.  You can set any value to the @samp{/ans} register in the current registry by using the @samp{answer} operation.
@example
... answer 10 .
ans = 10
@end example

The @samp{/ans} register is special because, after an Instruction finishes running in its temporary registry, whatever the value at the @samp{/ans} register is in that registry is moved to the @samp{/ans} register in the registry that called the instruction.  For example,
@example
... set /add-one ( answer [ add t 1 . ] . ) .
... add-one /t 10 .
ans = 11
... print ans .
11
@end example

The other reason that the @samp{/ans} register is special is, as mentioned, closed parenthesis evaluate to the value the statements inside them set the @samp{/ans} register to.  Because @samp{subtract 5 3 .} sets the @samp{/ans} register to @samp{2} and @samp{multiply 2 3 .} sets the @samp{/ans} register to @samp{6}, @samp{add [ subtract 5 3 . ] [ multiply 2 3 . ] .} sets the @samp{/ans} register to @samp{8}.

@item
Curly parenthesis ("@{@}") also enclose statements, but those statements are only evaluated @emph{if} we try to use the value of that expression which is, like for the closed parenthesis, the value the statements set the @samp{/ans} register to.  This form of evaluation is often called "lazy evaluation". We can think of closed parenthesis as marking expressions we want to "greedily evaluate" and curly parenthesis as marking expressions to "lazily evaluate".  Lazy evaluation is useful for the @samp{if} operation and other, similar instructions.  For example,
@example
 ... set /gt-4
 (
   set /str-t [ to-string t . ] . 
   if [ gt t 4 . ]
      @{ print [ string-append str-t " is greater than 4.\n" . ] . @}
      @{ print [ string-append str-t " is less than or equal to 4.\n" . ] . @} .
 ) .
... gt-4 /t 10 .
10 is greater than 4.
... gt-4 /t 3 .
3 is less than or equal to 4.
@end example

It works this way because the @samp{if} operation returns the second value if the first value is @samp{True} and the third value if the first value is @samp{False}.  So it tries to access the second value when @samp{t} is @samp{10}, executing the statements in the second argument, and similarly for the third argument when @samp{t} is @samp{3}.

If we had instead used closed parenthesis, both statements would execute everytime regardless of the the truth of the first argument to @samp{if}.  If we used open parenthesis, an Instruction object would be returned instead of actually doing the printing (the instruction object could be called to actually do the print later).
@end itemize


The last syntax element is the comment.  To write a comment in ARBEL, use a single quote (@samp{'}) as the first character in a statement.  You can think about this quote as an a special operator.  Comments end with a new line. The comment operator, like other syntax elements, needs whitespace after it, for example:
@example
... ' This is a correct comment
... 'This is an incorrect comment, 'This is interpreted as a reference to data in the Register /'This
@end example

That is really it as far as syntax is concerned.  Periods terminate statements, parenthesis enclose statements controlling when they are executed, and registers start with a forward slash and their values are obtained by omitting the slash.  We now also know how to define our own instructions and how to call them and how to prepare statements for greedy or lazy evaluation.

@node Registries
@section Registries

In ARBEL, most of the action happens inside @emph{registries}.  Registries are both data structures and define what registers (variables) are in scope.  Because they are full fledged data structures, you can switch scope easily or even pass a scope as an argument. When you start the interpreter, you are already inside the default, top-level registry.  To create a new one, use the @samp{registry} operation,

@example
... registry /hello "Hello, world!" /x 10 .
@end example

This @samp{registry} operation puts a new registry in the @samp{/ans} register.  The registry has a @samp{/hello} register bound to the value @samp{"Hello, world!"} and with the @samp{/x} register bound to the value @samp{10}.  Usually, you will want to bind the value to another register besides @samp{/ans} which will be overwritten by subsequent statements. You can do either of:

@example
... registry /hello "Hello, world!" /x 10 .
... move /ans /my-registry .
... print my-registry .
... ' Or:
... set /my-registry [ registry /hello "Hello, world!" /x 10 . ] .
... print my-registry .
@end example

To access values in a registry, you can either use the @samp{get} operation or the "colon notation":

@example
... get /hello my-registry .
ans = Hello, world!
... print my-registry:/hello
Hello, world!
... print hello .
Error: Value at `/hello` not found.
@end example

From the last line, we can see that the value @samp{hello} exists only in the registry located at @samp{/my-registry} which we are not currently in.  So we cannot access its @samp{/hello} register directly.  We can change which registry we are in with the @samp{go-in} operation.

@example
... go-in hello .
... print hello .
Hello, world!
@end example

We can go back to the registry we were in previously by using the @samp{go-out} operation.

@example
... go-out .
... print hello 
Error: Value at `/hello` not found.
@end example

We can import values from one registry into their corresponding registers in the current registry by using the @samp{import} operation.

@example
... import my-registry .
... print hello
Hello, world!
@end example

We can also execute an instruction inside a certain registry by using the @samp{in} command.

@example
... in my-registry ( print [ string-append hello " Goodbye!" . ] . ) .
Hello, world! Goodbye!
@end example



Aside from being a useful data structure, registries are also how ARBEL does @emph{namespaces}.  For example,

@example
... set /math [ registry . ] .
... set /factorial
       (
         set /i 2 .
         set /prod 1 .
         while ( lt-eq i t . )
               (
                 set /prod [ multiply i prod . ] .
                 set /i [ add i 1 . ] .
               ) .
         answer prod .
       ) math .
... math:/factorial /t 5 .
ans = 120
@end example

You can assign Instructions inside registries and then they can be called from that registry using the colon notation.  This code block also demonstrates that the @samp{set} operation takes a third argument: the registry in which to set the register.  When omitted, it sets the register in the current registry as we have seen above.

Of course, if you don't need to worry about name collisions, you can just @samp{import} it.

@example
... import math .
... factorial /t 5 .
ans = 120
@end example



Registries are flexible data structures.  They can be used to represent structures with multiple kinds of values (like @samp{struct} in C).  Or they can be used as namespaces for libraries or different parts of a code base.

@menu
* Additional details about colon notation::
@end menu

@node Additional details about colon notation
@subsection Additional details about colon notation

The colon notation can be nested.

@example
... set /x [ registry /y [ registry /z 10 . ] . ] .
... print x:/y:/z .
10
@end example

It can also choose the register by referencing the value of another register.
@example
... set /x [ registry /y 10 . ] .
... set /z /y .
... print x:z .
10
@end example

@emph{But} we cannot use subexpressions in the colon notation:
@example
... set /x [ registry /y 10 . ] .
... ' BAD! print x:[ answer /y . ] .
@end example

@node Registers
@section Registers

Registers are the locations of data in Registries.  They are data themselves and can be manipulated like other data objects in ARBEL.  For example, you can set other registers to registers and use those values wherever you would use Registers.

@example
... set /x /y .
... print x .
/y
... set x "hello" .
... print x .
/y
... print y .
hello
@end example

You can move data between Registers with the @samp{move} command:

@example
... set /x "hello" .
... move /x /y .
... print y .
hello
... print x .
Error: Value at register /x not found.
@end example

Registers are data denoting locations so they are often useful ways to manipulate the locations of values instead of the values themselves, as with the @samp{move} and @samp{set} operations.

@node Environment
@section Environment

ARBEL is not only a programming language. It is also an @emph{environment}.  The current state of ARBEL --- the values and where they are located in the registry --- can be saved to disk and re-loaded later on.

@node Examples
@chapter Examples

@menu
* A Line-Oriented Text Editor::
* Numerical Differentiation::
* Dispatch::
* Compute Factorial::
* Numerical Integration::
* Basic Linear Algebra::
* Split a String::
@end menu

@node A Line-Oriented Text Editor
@section A Line-Oriented Text Editor

@example
' Line-oriented text editor
set /arbed [ registry . ] .
in arbed
   (
     set /buffer [ list . ] . 
     set /cur-line 1 . 
     set /n ( up set /cur-line [ add cur-line 1 . ] . ) .
     set /p ( up set /cur-line [ sub cur-line 1 . ] . ) .
     set /i
         (
           set /arbed-str s buffer .
           in buffer
              (
                to-register [ string-append
                                     "#" [ to-string cur-line . ] . ] .
                set ans arbed-str .
              ) .
         ) .
     set /o
         (
           to-register [ string-append
                                "#" [ to-string cur-line . ] . ] .
           print [ get ans buffer . ] .
         ) .
   ) .
@end example

Example of use:
@example
... go-in arbed .
... i /s "Hello" .
... o .
Hello
... n .
... i /s "Goodbye" .
... o .
Goodbye
... p .
... o .
Hello
@end example

@node Numerical Differentiation
@section Numerical Differentiation

@example
' Compute the numerical derivative of a function
set /deriv
    (
      if [ not [ exist /h . ] . ]
         @{
           set /h 0.001 .
         @} .
      div [ sub [ f /t [ add x h . ] . ]
                [ f /t [ sub x h . ] . ] . ]
          [ mul 2 h . ] .
    ) .
@end example

Example of use:
@example
... set /f ( power t 2 . ) .
... deriv /f f /x 1.0
ans = 2.0
@end example

@node Dispatch
@section Dispatch

@example
' Demonstrates dispatching to different types

set /add-n-times
    (
      dispatch "add-n-times" val .
      ans /val val /times times .
    ) .

set /add-n-times+Integer
    (
      mul val times .
    ) .

set /add-n-times+Real add-n-times+Integer .

set /add-n-times+String
    (
      set /result val .
      repeat [ sub times 1 . ]
             ( set /result [ string-append result val . ] . ) .
      answer result .
    ) .
@end example

Example of use:
@example
... add-n-times /val 4 /times 5 .
ans = 20
... add-n-times /val 4.0 /times 5 .
ans = 20.0
... add-n-times /val "hello" /times 5 .
ans = hellohellohellohellohello
@end example

@node Compute Factorial
@section Compute Factorial

@example
' compute a factorial using while
set /factorial
    (
      set /i 2 .
      set /prod 1 .
      while ( lt-eq i t . )
            (
              set /prod [ mul i prod . ] .
              set /i [ add i 1 . ] .
            ) .
      answer prod .
    ) .


' compute factorial using recursion

set /factorial-recurse
    (
      if [ eq t 1 . ]
         1
         @{
           mul t [ factorial-recurse /t [ sub t 1 . ] . ] .
         @} .
    ) .
@end example

Example of use:
@example
... factorial 5 .
ans = 120
@end example

@node Numerical Integration
@section Numerical Integration

@example
' takes a function as an argument and return left hand
' riemmann sum over range

set /integrate 
    ( 
      set /delta [ div [ sub ub lb . ] n . ] .
      set /x lb .
      set /int 0.0 .
      repeat n
            (
              set /int [ add int [ f /t x . ] . ] .
              set /x [ add x delta . ] .
            ) .
      mul int delta .
    ) .
@end example

Example of use:
@example
... integrate /f ( power t 2 . ) /lb 0.0 /ub 1.0 /n 1000 .
ans = 0.332834
@end example

@node Basic Linear Algebra
@section Basic Linear Algebra

@example
' Computes v^T w for a vector 
set /dotproduct
    (
      do-to-all ( mul t1 t2 . ) v w .
      collapse ( add t1 t2 . ) ans "#" .
    ) .

' matrix-multiply A B computes A^T B 
set /matrix-multiply
    (
      set /i /#1 .
      set /out [ list . ] .
      while ( exist i B . )
            (
              do-to-all ( dotproduct /v t1 /w B:i . ) A .
              move /ans /res .
              in out ( set i res . ) .
              next i .
              move /ans /i .
            ) .
      answer out .
    ) .

' Computes A^T 
set /transpose
    (
      set /i /#1 .
      set /out [ list . ] .
      while ( exist i A:/#1 . )
            (
              do-to-all ( answer t1:i . ) A .
              move /ans /res .
              in out ( set i res . ) .
              next i .
              move /ans /i .
            ) .
      answer out .
    ) .
@end example

Example of use:
@example
... set /A [ list [ list 1 2 . ] [ list 3 4 . ] . ] .
... set /B [ list [ list 6 2 . ] [ list 3 4 . ] . ] .
... transpose /A A .
ans = a registry with:
t1 of type Registry, value: a registry with:
t1 of type Integer, value: 1
t2 of type Integer, value: 3
t2 of type Registry, value: a registry with:
t1 of type Integer, value: 2
t2 of type Integer, value: 4
... matrix-multiply /A A /B B .
ans = a registry with:
t1 of type Registry, value: a registry with:
t1 of type Integer, value: 10
t2 of type Integer, value: 26
t2 of type Registry, value: a registry with:
t1 of type Integer, value: 11
t2 of type Integer, value: 25
@end example

@node Split a String
@section Split a String

@example
' Tokenize a string /line by splitting on string /sep
set /tokenize
    (
      set /i 1 .
      set /len [ string-length line . ] .
      set /cur-reg /#1 .
      set /tokened [ list . ] .
      set /init 1 .
      while ( lt-eq i len . )
            (
              set /d [ substring line i i . ] .
              if [ string-eq d sep . ]
                 @{
                   in tokened ( set cur-reg buffer . ) .
                   set /cur-reg [ next cur-reg . ] .
                   set /init 1 .
                 @}
                 @{
                   if [ eq init 1 . ] @{ set /buffer d . set /init 0 . @}
                      @{ set /buffer [ string-append buffer d . ] . @} .
                 @} .
              set /i [ add i 1 . ] .
            ) .
      in tokened ( set cur-reg buffer . ) .
      answer tokened .
    )  .
@end example

Examples of use:
@example
... tokenize /line "1,2,3" /sep "," .
ans = a registry with:
t1 of type String, value: 1
t2 of type String, value: 2
t3 of type String, value: 3
@end example

@node Reference
@chapter Reference

Arguments that are optional are enclosed in angle brackets (<>).  Operations are classified by the main data type they manipulate.

@menu
* Registry operations::
* Real and Integer operations::
* Control flow operations::
* Environment operations::
* String operations::
* Register operations::
* Instruction operations::
* File operations::
* Boolean operations::
* Column operations::
@end menu

@node Registry operations
@section Registry operations

@menu
* Creation::
* Insert, move, and remove data to and from registers: Insert move and remove data to and from registers. 
* Access data in Registry::
* Apply Instructions to elements of a Registry::
* Execute code in a registry::
* Test if a registry::
* Classify a registry::
@end menu

@node Creation
@subsection Creation

@itemize
@item
@samp{registry REGISTER1 VALUE1 REGISTER2 VALUE2 ...} --- sets the @samp{/ans} register to a registry with @samp{VALUE1} located at @samp{REGISTER1} and so on.

@item
@samp{list VALUE1 VALUE2 ...} --- set the @samp{/ans} register to a Registry with @samp{VALUE1} at Register @samp{/t1}, @samp{VALUE2} at Register @samp{/t2}, and so on.
@end itemize

@node Insert move and remove data to and from registers
@subsection Insert, move, and remove data to and from registers

@itemize
@item
@samp{set REGISTER VALUE <REGISTRY>} --- sets the value at @samp{REGISTER} to @samp{VALUE} in the registry @samp{REGISTRY}.  If the @samp{REGISTRY} argument is omitted, then it will set the register in the current registry.

@item
@samp{move REGISTER1 REGISTER2} --- move the value located at @samp{REGISTER1} to @samp{REGISTER2}.  Does not copy the data so it is an efficient way to move data.

@item
@samp{delete REGISTER} --- delete the value at @samp{REGISTER} in the current registry.

@item
@samp{free REGISTER} --- delete the value at @samp{REGISTER} in the current registry.  Does not send the free'd data to the garbage collector but instead releases the memory immediately.  The trade-off versus @samp{delete} is that it forces immediate removal of memory in main execution thread (which takes longer and blocks other instructions) while @samp{delete} is faster but the memory may not be immediately removed (higher memory use).
@end itemize

@node Access data in Registry
@subsection Access data in Registry

@itemize
@item
@samp{get REGISTER <REGISTRY>} --- sets the @samp{/ans} register to the value located at @samp{REGISTER} in @samp{REGISTRY}.  If the @samp{REGISTRY} argument is not specified, get from the current registry.

@item
@samp{exist REGISTER <REGISTRY>} --- set the @samp{/ans} register to @samp{True} if a value exists at the @samp{REGISTER} in @samp{REGISTRY}.  If the @samp{REGISTRY} argument is omitted, check in the current registry.

@item
@samp{import REGISTRY} --- set the Registers in the current Registry to hold the same values that they hold in REGISTRY.

@item
@samp{where REGISTRY INSTRUCTION} --- set the @samp{/ans} register to a Registry containing all the elements in the registry such that the INSTRUCTION when passed the element of the Registry at Register @samp{/t} sets the @samp{/ans} register to @samp{True}.
@end itemize

@node Apply Instructions to elements of a Registry
@subsection Apply Instructions to elements of a Registry

@itemize
@item
@samp{do-to-all INSTRUCTION REGISTRY1 REGISTRY2 ... REGISTRYN} --- execute @samp{INSTRUCTION} which takes arguments at Registers @samp{/t1}, @samp{/t2}, @dots{}, @samp{/tN} for each element in @samp{REGISTRY1}, @dots{}, @samp{REGISTRYN}.  Set the @samp{/ans} register to a Registry which contains whatever the @samp{INSTRUCTION} evaluates to at the corresponding Registers.  For example, @samp{do-to-all ( add t1 t2 . ) [ list 1 2 3 . ] [ list 4 5 6 . ]} sets the @samp{/ans} register to a Registry with elements (5,7,9) at registers @samp{(/t1,/t2,/t3)}.  @samp{do-to-all} will only return results at Registers that exist in all Registries.  So, for example, @samp{do-to-all ( add t1 t2 . ) [ list 1 2 3 . ] [ list 4 5 . ]} sets the @samp{/ans} Register to @samp{list 5 7}.

@item
@samp{collapse INSTRUCTION REGISTRY <STRING>} --- sets the @samp{/ans} register to the value of applying @samp{INSTRUCTION} to the value at Register @samp{/STRING1} and @samp{/STRING2} and then again to the result of that and to the value of  @samp{/STRING3} and so on where the @samp{INSTRUCTION} takes each argument at registers @samp{/t1} and @samp{/t2}. For example, @samp{collapse ( add t1 t2 . ) [ list 1 2 3 . ] "t"} sets the @samp{/ans} register to @samp{6}.  If the @samp{STRING} argument is omitted, it is assumed to be @samp{"t"}.
@end itemize

@node Execute code in a registry
@subsection Execute code in a registry

@itemize
@item
@samp{in REGISTRY INSTRUCTION} --- call @samp{INSTRUCTION} in @samp{REGISTRY}.

@item
@samp{go-in REGISTRY} --- make @samp{REGISTRY} the new current registry.

@item
@samp{go-out} --- leave the current Registry and move to the Registry above it.

@item
@samp{up STATEMENT} --- execute STATEMENT in the registry above the current one after resolving the values of the arguments of the statement in the current registry.  For example, we can create an Instruction that can be used to increment a variable in place, like so: 
@example
... set /inc ( up set x [ add 1 [ up get x . ] . ] . ) .
... set /x 1 . 
... inc /x /x . 
... print x .
2
@end example
@end itemize

@node Test if a registry
@subsection Test if a registry

@itemize
@item
@samp{is-registry VALUE} --- sets the @samp{/ans} Register to @samp{True} if the @samp{Value} is a Registry and to @samp{False} otherwise.
@end itemize

@node Classify a registry
@subsection Classify a registry

@itemize
@item
@samp{of STRING REGISTRY} --- declare REGISTRY to be @samp{of} kind STRING.

@item
@samp{is-of STRING REGISTRY} --- set the @samp{/ans} Register to @samp{True} if REGISTRY is of kind STRING.

@item
@samp{dispatch STRING VALUE} --- set the @samp{/ans} Register to the Instruction called STRING corresponding to VALUE.  In other words if @samp{dispatch "f" 10}, sets the @samp{/ans} register to Instruction at @samp{/f+Integer}.
@end itemize

@node Real and Integer operations
@section Real and Integer operations

@menu
* Arithmetic operations::
* Comparison operations::
* Conversion operations::
* Test if type operations::
* Common mathematical operations::
@end menu

@node Arithmetic operations
@subsection Arithmetic operations

@itemize
@item
@samp{add NUMBER1 NUMBER2 ...} --- adds all the numbers together and sets the @samp{/ans} register to the result.

@item
@samp{mul NUMBER1 NUMBER2 ...} --- multiplies all the numbers together and sets the @samp{/ans} register to the result.

@item
@samp{sub NUMBER1 NUMBER2 ...} --- subtracts the second number from the first and the third number from that and so on and sets the @samp{/ans} register to the result. 

@item
@samp{div NUMBER1 NUMBER2 ...} --- divides the first number by the second, the result by the third number, and so on and sets the @samp{/ans} register to the result.

@item
@samp{mod NUMBER1 NUMBER2} --- return the remainder of dividing the first number by the second.
@end itemize

@node Comparison operations
@subsection Comparison operations

@itemize
@item
@samp{gt NUMBER1 NUMBER2} --- set the @samp{/ans} register to @samp{True} if @samp{NUMBER1} is greater than @samp{NUMBER2} and to @samp{False} otherwise.

@item
@samp{lt NUMBER1 NUMBER2} --- set the @samp{/ans} register to @samp{True} if @samp{NUMBER1} is less than @samp{NUMBER2} and to @samp{False} otherwise.

@item
@samp{eq NUMBER1 NUMBER2} --- set the @samp{/ans} register to @samp{True} if @samp{NUMBER1} is equal to @samp{NUMBER2} and to @samp{False} otherwise.

@item
@samp{lt-eq NUMBER1 NUMBER2} --- set the @samp{/ans} register to @samp{True} if @samp{NUMBER1} is less than or equal to @samp{NUMBER2} and to @samp{False} otherwise.

@item
@samp{gt-eq NUMBER1 NUMBER2} --- set the @samp{/ans} register to @samp{True} if @samp{NUMBER1} is greater than or equal to @samp{NUMBER2} and to @samp{False} otherwise.
@end itemize

@node Conversion operations
@subsection Conversion operations

@itemize
@item
@samp{to-number STRING|REGISTER} --- if the argument is a String, try to convert to a number and set the @samp{/ans} Register to the result.  If the argument is a Register and ends in a number, set the @samp{/ans} Register to the result.

@item
@samp{to-real INTEGER} --- set the @samp{/ans} Register to a Real representing the INTEGER.
@end itemize

@node Test if type operations
@subsection Test if type operations

@itemize
@item
@samp{is-integer VALUE} --- sets the @samp{/ans} Register to @samp{True} if the @samp{Value} is an Integer and to @samp{False} otherwise.

@item
@samp{is-real VALUE} --- sets the @samp{/ans} Register to @samp{True} if the @samp{Value} is a Real and to @samp{False} otherwise.
@end itemize

@node Common mathematical operations
@subsection Common mathematical operations

@itemize
@item
@samp{log NUMBER} --- set the @samp{/ans} Register to the natural logarithm of NUMBER.

@item
@samp{exp NUMBER} --- set the @samp{/ans} Register to the NUMBER power of the natural base.

@item
@samp{power NUMBER1 NUMBER2} --- set the @samp{/ans} Register to NUMBER1 raised to the NUMBER2 power.
@end itemize

@node Control flow operations
@section Control flow operations

@itemize
@item
@samp{if BOOLEAN VALUE1 <VALUE2>} --- if the first argument is @samp{True}, sets the @samp{/ans} register to @samp{VALUE1}, if it is @samp{False}, sets the @samp{/ans} register to @samp{VALUE2}.  If @samp{VALUE2} is omitted, do nothing if the first argument is @samp{False}.

@item
@samp{while INSTRUCTION1 INSTRUCTION2} --- Call @samp{INSTRUCTION1} in the current Registry. If it sets its @samp{/ans} register to @samp{True}, call @samp{INSTRUCTION2}.  Repeat.

@item
@samp{repeat INTEGER INSTRUCTION} --- call INSTRUCTION INTEGER times.
@end itemize

@node Environment operations
@section Environment operations

@menu
* Modify the /ans register::
* Exit ARBEL::
* Print output::
* Input and output files and state::
* Interacting with the shell::
* Error handling::
@end menu

@node Modify the /ans register
@subsection Modify the /ans register

@itemize
@item
@samp{answer VALUE} --- set the @samp{/ans} register to @samp{VALUE}.

@item
@samp{sit} --- do nothing but lookup all arguments (causes Active Expressions --- instructions in curly brackets --- to be executed).
@end itemize

@node Exit ARBEL
@subsection Exit ARBEL

@itemize
@item
@samp{exit} --- exit ARBEL.
@end itemize

@node Print output
@subsection Print output

@itemize
@item
@samp{print VALUE} --- prints @samp{VALUE} to screen.
@end itemize

@node Input and output files and state
@subsection Input and output files and state

@itemize
@item
@samp{source STRING} --- executes ARBEL code in the file named by STRING.

@item
@samp{save FILENAME} --- save the contents of the current registry to file.

@item
@samp{load FILENAME} --- load the contents of a saved file into the environment.

@item
@samp{output-code STRING} --- outputs the code that has been entered at the prompt so far to the file named STRING.

@item
@samp{clear-code} --- clear code that has been entered so far so that the code that @samp{output-code} outputs will be empty.
@end itemize

@node Interacting with the shell
@subsection Interacting with the shell

@itemize
@item
@samp{shell STRING} --- execute the shell command STRING.

@item
@samp{change-dir STRING} --- change directory to the location given in STRING.

@item
@samp{current-dir} --- set the @samp{/ans} Register to a String giving the current directory.
@end itemize

@node Error handling
@subsection Error handling

@itemize
@item
@samp{error STRING <INTEGER>} --- outputs error message String and sets an error code INTEGER (if specified, otherwise, the error number is @samp{1}).

@item
@samp{is-error INSTRUCTION} --- sets the @samp{/ans} Register to @samp{True} if there was an error executing the @samp{INSTRUCTION} and to @samp{False} otherwise.

@item
@samp{push-through INSTRUCTION} --- execute each statement in @samp{INSTRUCTION} even if there is one of the statements has an error.
@end itemize

@node String operations
@section String operations

@menu
* Access, search, and modify string elements: Access search and modify string elements. 
* String properties::
* String comparison::
* Combine strings::
* Convert to string::
* Test if String::
* Strings from User Input::
@end menu

@node Access search and modify string elements
@subsection Access, search, and modify string elements

@itemize
@item
@samp{substring STRING INTEGER1 INTEGER2} --- sets the @samp{/ans} Register to the subset of STRING where the characters included are determined by INTEGER1 and INTEGER2.  Strings are 1-indexed in ARBEL so the first character is at location 1.  If the INTEGER is less than or equal to 0, determine the location from the end of the String.  So if @samp{INTEGER1} and @samp{INTEGER2} are @samp{0}, then it will set the @samp{/ans} register to the last character in @samp{STRING}.

@item
@samp{match STRING1 STRING2 <INTEGER>} --- find occurences of regular expression STRING1 in String STRING2.  Sets the @samp{/ans} Register to a Registry with all submatches located at the @samp{/tN} registries.  The optional third argument gives the number of matches to return.  If @samp{0} is provided, return all matches (default).

@item
@samp{replace STRING1 STRING2 STRING3 <INTEGER>} --- set the @samp{/ans} Register to a String formed by replacing all occurences of regular expression STRING1 with STRING2 in STRING3.  The optional fourth argument gives the number of matches to replace.  If @samp{0}, replace all (default).
@end itemize

@node String properties
@subsection String properties

@itemize
@item
@samp{string-length STRING} --- sets the @samp{/ans} register to the number of characters in @samp{STRING}.
@end itemize

@node String comparison
@subsection String comparison

@itemize
@item
@samp{string-eq STRING1 STRING2} --- sets the @samp{/ans} Register to @samp{True} if the two strings are equal and to @samp{False} otherwise.

@item
@samp{string-lt STRING1 STRING2} --- sets the @samp{/ans} Register to @samp{True} if @samp{STRING1} is less than @samp{STRING2} in the sense that the ASCII characters have lower values.

@item
@samp{string-gt STRING1 STRING2} --- sets the @samp{/ans} Register to @samp{True} if @samp{STRING1} is greater than @samp{STRING2} in the sense that the ASCII characters have lower values.
@end itemize

@node Combine strings
@subsection Combine strings

@itemize
@item
@samp{string-append STRING1 STRING2} --- sets the @samp{/ans} register to the concatenation of the two Strings so that the resulting string is @samp{"STRING1STRING2"}.
@end itemize

@node Convert to string
@subsection Convert to string

@itemize
@item
@samp{to-string INTEGER|REAL|REGISTER <INTEGER2>} --- sets the @samp{/ans} Register to a String representing the INTEGER or REAL or REGISTER provided as the first argument.  The second argument is optional if a REAL argument is provided.  INTEGER2 gives the number of elements after the decimal point to include.  If not provided, 6 decimal places are included.
@end itemize

@node Test if String
@subsection Test if String

@itemize
@item
@samp{is-string VALUE} --- sets the @samp{/ans} Register to @samp{True} if the @samp{Value} is a String and to @samp{False} otherwise.
@end itemize

@node Strings from User Input
@subsection Strings from User Input

@itemize
@item
@samp{input REGISTER} --- reads a line of text the user enters and sets the @samp{/ans} Register to the result (always a String).
@end itemize

@node Register operations
@section Register operations

@menu
* Operations on "list" registers::
* Convert to register::
* Register comparison::
* Test if a Register::
@end menu

@node Operations on "list" registers
@subsection Operations on "list" registers

@itemize
@item
@samp{next REGISTER} --- if the @samp{REGISTER} ends in a number, return the Register with that number incremented by 1.

@item
@samp{previous REGISTER} --- if the @samp{REGISTER} ends in a number, return the Register with that number minus 1.  If that would cause the number to be less than 1 set the @samp{/ans} Register to the "first" register with that prefix so that @samp{previous /t1 .} sets @samp{/ans} to @samp{/t1}.

@item
@samp{last REGISTRY STRING} --- return the last Register in the @samp{REGISTRY} that starts with @samp{STRING}.  So that if you had registers @samp{/x0}, @samp{/x1}, and @samp{/x2} in the Registry, @samp{last REGISTRY "x"} would return @samp{/x2}.
@end itemize

@node Convert to register
@subsection Convert to register

@itemize
@item
@samp{to-register STRING|INTEGER} --- sets the @samp{/ans} register to a Register named @samp{STRING} or to @samp{/tINTEGER}.
@end itemize

@node Register comparison
@subsection Register comparison

@itemize
@item
@samp{register-eq REGISTER1 REGISTER2} --- sets the @samp{/ans} Register to @samp{True} if the two Registers are the same and to @samp{False} otherwise.
@end itemize

@node Test if a Register
@subsection Test if a Register

@itemize
@item
@samp{is-register VALUE} --- sets the @samp{/ans} Register to @samp{True} if the @samp{Value} is a Register and to @samp{False} otherwise.
@end itemize

@node Instruction operations
@section Instruction operations

@itemize
@item
@samp{call INSTRUCTION REGISTER1 VALUE1 REGISTER2 VALUE2 ...} --- executes INSTRUCTION in a Registry with REGISTER1 assigned to VALUE1 and so on.

@item
@samp{is-instruction VALUE} --- sets the @samp{/ans} Register to @samp{True} if the @samp{Value} is an Instruction and to @samp{False} otherwise.
@end itemize

@node File operations
@section File operations

@itemize
@item
@samp{is-file VALUE} --- sets the @samp{/ans} Register to @samp{True} if the @samp{Value} is a File and to @samp{False} otherwise.

@item
@samp{open-text-file STRING} --- sets the @samp{/ans} Register to a File object corresponding to the file named STRING.

@item
@samp{read FILE} --- read a single character from a File object and set the @samp{/ans} Register to that character.

@item
@samp{close REGISTER} --- if a File object is located at REGISTER, close the File and remove its reference from the REGISTER.

@item
@samp{read-line FILE} --- sets the @samp{/ans} Register to the next line from FILE.

@item
@samp{write STRING FILE} --- write a given STRING to FILE.
@end itemize

@node Boolean operations
@section Boolean operations

@itemize
@item
@samp{is-boolean VALUE} --- sets the @samp{/ans} Register to @samp{True} if the @samp{Value} is a Boolean and to @samp{False} otherwise.

@item
@samp{and BOOLEAN1 BOOLEAN2} --- sets the @samp{/ans} Register to @samp{True} if BOOLEAN1 and BOOLEAN2 are @samp{True} and to @samp{False} otherwise.

@item
@samp{or BOOLEAN1 BOOLEAN2} --- sets the @samp{/ans} Register to @samp{True} if either BOOLEAN1 or BOOLEAN2 are @samp{True} and to @samp{False} otherwise.

@item
@samp{not BOOLEAN} --- sets the @samp{/ans} Register to @samp{True} if BOOLEAN is @samp{False} and to @samp{False} otherwise.
@end itemize

@node Column operations
@section Column operations

@itemize
@item
@samp{fill INTEGER VALUE} --- sets the @samp{/ans} Register to a Column of size @samp{INTEGER} where each element is set to @samp{VALUE}.

@item
@samp{element COLUMN INTEGER} --- sets the @samp{/ans} Register to the @samp{INTEGER} element of the Column @samp{COLUMN}.

@item
@samp{modify-at COLUMN INTEGER VALUE} --- sets the value in @samp{COLUMN} at location @samp{INTEGER} to @samp{VALUE}.

@item
@samp{transform INSTRUCTION COLUMN1 ... COLUMNN} --- sets the @samp{/ans} Register to a Column formed by evaluating @samp{INSTRUCTION} after setting the registers @samp{/t1,.../tN} to each element in @samp{COLUMN1,...,COLUMNN}.

@item
@samp{height COLUMN} --- return the number of elements in @samp{COLUMN}.
@end itemize

@bye